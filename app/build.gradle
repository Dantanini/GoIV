buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'
apply from: '../checkstyle/checkstyle.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


def keystoreProperties = new Properties()

if (new File('keystore.properties').exists()) {
    def keystorePropertiesFile = rootProject.file('keystore.properties')
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.keyAlias = 'your_key_alias'
    keystoreProperties.keyPassword = 'your_key_password'
    keystoreProperties.storeFile = 'your_keystore.jks'
    keystoreProperties.storePassword = 'your_keystore_password'
}

android {

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    //When making a new version, please ensure that the apk files for both online and offline flavors
    //are in the same GitHub release and the offline apk filename has "Offline" in it.

    defaultConfig {
        resValue "string", "app_name", "GoIV"
        applicationId "com.kamron.pogoiv"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 16
        versionName "3.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        offline {
            applicationId "com.kamron.pogoiv.nointernet"
            buildConfigField "boolean", "isInternetAvailable", "false"
            resValue "string", "app_name", "GoIV Offline"
            ext.enableCrashlytics = false
        }

        online {
            applicationId "com.kamron.pogoiv"
            buildConfigField "boolean", "isInternetAvailable", "true"
            resValue "string", "app_name", "GoIV"
            ext.enableCrashlytics = true
        }
    }

    buildTypes {
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
            signingConfig signingConfigs.releaseConfig
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.rmtheis:tess-two:6.0.3'
    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'io.apptik.widget:multislider:1.2'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    apt "org.projectlombok:lombok:1.16.10"
    provided "org.projectlombok:lombok:1.16.10"

    // only include crashlytics in the online build
    onlineCompile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.android.support:support-annotations:24.2.0'
    testCompile 'com.android.support.test:runner:0.5'

    androidTestCompile 'com.android.support:support-annotations:24.2.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
}
